buildscript {

    ext {
        kotlinVersion = '1.6.10'
        kotlinTestrunnerVersion = '4.6.4' // https://github.com/gradle/gradle/issues/16345#issuecomment-861119625
        gradleVersion = '7.3.3'
    }
}

plugins {
    id 'idea'
    id 'eclipse'
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id 'com.gradle.plugin-publish' version '0.20.0'
    id 'com.github.ben-manes.versions' version '0.41.0'
}

sourceCompatibility = 1.8

dependencies {
    compileOnly gradleApi()
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "com.google.code.gson:gson:2.8.9"

    testCompileOnly gradleTestKit()
    testImplementation "io.kotest:kotest-runner-junit5-jvm:$kotlinTestrunnerVersion"
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        dependencycheckplugin {
            id = 'org.entur.plugins.orbdependencycheck'
            displayName = 'CircleCi Orb version management'
            description = 'Gradle plugin to check the orbs defined in your circleci config against the latest version in the circleci orb registry'
            implementationClass = 'org.entur.plugins.OrbDependencyCheckPlugin'
        }
    }
}


wrapper {
    gradleVersion = "$gradleVersion"
}

pluginBundle {
    website = 'https://www.entur.org'
    vcsUrl =  'https://github.com/entur/Gradle-CircleCI-Orb-Dependency-Check-Plugin'
    tags = ['circleci', 'orb', 'dependency', 'versions']
}

idea {
    module {
        outputDir file('build/classes/main')
        testOutputDir file('build/classes/test')
    }
}